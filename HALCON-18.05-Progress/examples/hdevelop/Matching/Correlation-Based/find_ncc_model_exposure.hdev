<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="18.05">
<procedure name="main">
<interface/>
<body>
<c>* This example program shows how to use HALCON's correlation-based</c>
<c>* matching. In particular it demonstrates the robustness of this method against</c>
<c>* linear illumination changes.  The training is performed in an image with good</c>
<c>* illumination.  The matching is applied in images where the exposure time varies</c>
<c>* extremely from very short to very long.</c>
<l>read_image (Image, 'cap_exposure/cap_exposure_03')</l>
<l>dev_close_window ()</l>
<l>dev_open_window_fit_image (Image, 0, 0, -1, -1, WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_update_off ()</l>
<l>gen_circle (Circle, 246, 336, 150)</l>
<l>area_center (Circle, Area, RowRef, ColumnRef)</l>
<l>reduce_domain (Image, Circle, ImageReduced)</l>
<l>create_ncc_model (ImageReduced, 'auto', 0, 0, 'auto', 'use_polarity', ModelID)</l>
<l>dev_set_draw ('margin')</l>
<l>dev_display (Image)</l>
<l>dev_set_color ('yellow')</l>
<l>dev_display (Circle)</l>
<l>disp_message (WindowHandle, 'Trained NCC model', 'window', 12, 12, 'black', 'true')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<l>Rows := []</l>
<l>Cols := []</l>
<l>for J := 1 to 10 by 1</l>
<l>    read_image (Image, 'cap_exposure/cap_exposure_' + J$'02')</l>
<l>    find_ncc_model (Image, ModelID, 0, 0, 0.5, 1, 0.5, 'true', 0, Row, Column, Angle, Score)</l>
<l>    Rows := [Rows,Row]</l>
<l>    Cols := [Cols,Column]</l>
<l>    dev_display (Image)</l>
<l>    dev_display_ncc_matching_results (ModelID, 'green', Row, Column, Angle, 0)</l>
<l>    disp_message (WindowHandle, 'Found NCC model', 'window', 12, 12, 'black', 'true')</l>
<l>    if (J &lt; 10)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>    endif</l>
<l>    stop ()</l>
<l>endfor</l>
<c>* Compute the standard deviation of the found positions.  If the individual</c>
<c>* positions in Rows and Cols are examined, it can be seen that the standard</c>
<c>* deviation is caused mainly by the last four images, which are severely</c>
<c>* overexposed.</c>
<l>StdDevRows := deviation(Rows)</l>
<l>StdDevCols := deviation(Cols)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
