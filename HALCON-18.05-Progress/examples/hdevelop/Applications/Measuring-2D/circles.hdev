<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="18.05">
<procedure name="main">
<interface/>
<body>
<c>* </c>
<c>* The edges in the image are segmented into lines and circles.</c>
<c>* For the edges that are part of a circle, the circle parameters</c>
<c>* are estimated and the resulting circle is displayed.</c>
<l>read_image (Image, 'double_circle')</l>
<c>* </c>
<c>* Init window</c>
<l>dev_close_window ()</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_open_window (0, 0, Width, Height, 'black', WindowHandle)</l>
<c>* </c>
<c>* Segment a region containing the edges</c>
<l>fast_threshold (Image, Region, 0, 120, 7)</l>
<l>boundary (Region, RegionBorder, 'inner')</l>
<l>clip_region_rel (RegionBorder, RegionClipped, 5, 5, 5, 5)</l>
<l>dilation_circle (RegionClipped, RegionDilation, 2.5)</l>
<l>reduce_domain (Image, RegionDilation, ImageReduced)</l>
<c>* </c>
<c>* In the subdomain of the image containing the edges,</c>
<c>* extract subpixel precise edges.</c>
<l>edges_sub_pix (ImageReduced, Edges, 'canny', 2, 20, 60)</l>
<l>segment_contours_xld (Edges, ContoursSplit, 'lines_circles', 5, 4, 3)</l>
<l>count_obj (ContoursSplit, Number)</l>
<l>dev_display (Image)</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_color ('white')</l>
<l>dev_update_window ('off')</l>
<l>for I := 1 to Number by 1</l>
<l>    select_obj (ContoursSplit, ObjectSelected, I)</l>
<l>    get_contour_global_attrib_xld (ObjectSelected, 'cont_approx', Attrib)</l>
<c>    * Fit a circle to the line segment that are arcs of a circle</c>
<l>    if (Attrib &gt; 0)</l>
<l>        fit_circle_contour_xld (ObjectSelected, 'ahuber', -1, 2, 0, 3, 2, Row, Column, Radius, StartPhi, EndPhi, PointOrder)</l>
<l>        gen_circle_contour_xld (ContCircle, Row, Column, Radius, 0, rad(360), 'positive', 1.0)</l>
<l>        dev_display (ContCircle)</l>
<l>    endif</l>
<l>endfor</l>
<l>dev_set_colored (12)</l>
<l>dev_set_line_width (3)</l>
<l>dev_display (ContoursSplit)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
