<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="18.05">
<procedure name="main">
<interface/>
<body>
<c>* In this program the size of raw fish sticks is measured</c>
<c>* before they are coated with bread crumbs.</c>
<c>* First, the fish sticks are segmented from the background.</c>
<c>* Then, their dimensions are measured, compared to the</c>
<c>* required minimum size, and the result is displayed.</c>
<c>* </c>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>read_image (Image, 'food/fish_sticks_raw_01')</l>
<l>dev_open_window_fit_image (Image, 0, 0, -1, -1, WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_set_draw ('margin')</l>
<l>get_system ('store_empty_region', StoreEmptyRegion)</l>
<l>set_system ('store_empty_region', 'true')</l>
<c>* </c>
<l>cm_per_pix := 0.0373</l>
<l>TargetLength := 9.0</l>
<l>ToleranceHigh := 0.8</l>
<l>ToleranceLow := 0.2</l>
<c>* </c>
<l>NumImages := 10</l>
<l>for I := 1 to NumImages by 1</l>
<l>    read_image (Image, 'food/fish_sticks_raw_' + I$'.2')</l>
<c>    * </c>
<c>    * Segment the raw fish sticks</c>
<l>    threshold (Image, Region, 50, 255)</l>
<l>    connection (Region, ConnectedRegions)</l>
<l>    fill_up (ConnectedRegions, RegionFillUp)</l>
<l>    select_shape (RegionFillUp, SelectedRegions, 'area', 'and', 1000, 99999)</l>
<c>    * </c>
<c>    * Select the regions that do not intersect the image border</c>
<l>    boundary (Image, RegionBorder, 'inner')</l>
<l>    intersection (SelectedRegions, RegionBorder, RegionIntersection)</l>
<l>    area_center (RegionIntersection, Area, Row1, Column1)</l>
<l>    select_mask_obj (SelectedRegions, ValidSticks, Area [==] 0)</l>
<l>    select_mask_obj (SelectedRegions, StickAtBorder, Area [!=] 0)</l>
<c>    * </c>
<c>    * Check the size of the fish sticks</c>
<l>    smallest_rectangle2 (ValidSticks, Row, Column, Phi, Length1, Length2)</l>
<l>    FishLength1 := Length1 * 2 * cm_per_pix</l>
<l>    Good := (FishLength1 [&gt;=] TargetLength - ToleranceLow) and (FishLength1 [&lt;=] TargetLength + ToleranceHigh)</l>
<l>    Bad := not Good</l>
<c>    * </c>
<c>    * Display the results</c>
<l>    RowGood := select_mask(Row,Good)</l>
<l>    ColumnGood := select_mask(Column,Good)</l>
<l>    RowBad := select_mask(Row,Bad)</l>
<l>    ColumnBad := select_mask(Column,Bad)</l>
<l>    FishLength1Good := select_mask(FishLength1,Good)</l>
<l>    FishLength1Bad := select_mask(FishLength1,Bad)</l>
<l>    gen_rectangle2 (AllSticks, Row, Column, Phi, Length1, Length2)</l>
<l>    select_mask_obj (AllSticks, GoodSticks, Good)</l>
<l>    select_mask_obj (AllSticks, BadSticks, Bad)</l>
<c>    * </c>
<l>    dev_display (Image)</l>
<l>    dev_set_color ('white')</l>
<l>    dev_set_line_width (1)</l>
<l>    dev_display (ValidSticks)</l>
<l>    dev_set_line_width (3)</l>
<l>    dev_set_color ('yellow')</l>
<l>    dev_display (StickAtBorder)</l>
<l>    dev_set_color ('green')</l>
<l>    dev_display (GoodSticks)</l>
<l>    dev_set_color ('red')</l>
<l>    dev_display (BadSticks)</l>
<l>    String := 'Target length: ' + TargetLength$'.1f' + ' cm'</l>
<l>    String[1] := 'Tolerance:  -' + ToleranceLow$'.1f' + '/+' + ToleranceHigh$'.1f'</l>
<l>    disp_message (WindowHandle, String, 'window', 12, 12, 'black', 'true')</l>
<l>    disp_message (WindowHandle, 'OK', 'image', 80, ColumnGood - 10, 'green', 'false')</l>
<l>    disp_message (WindowHandle, FishLength1Good$'.1f' + 'cm', 'image', RowGood, ColumnGood - 28, 'white', 'false')</l>
<l>    disp_message (WindowHandle, 'Not OK', 'image', 80, ColumnBad - 30, 'red', 'false')</l>
<l>    disp_message (WindowHandle, FishLength1Bad$'.1f' + 'cm', 'image', RowBad, ColumnBad - 28, 'white', 'false')</l>
<l>    if (I != NumImages)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
<l>set_system ('store_empty_region', StoreEmptyRegion)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
