<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="18.05">
<procedure name="main">
<interface/>
<body>
<l>dev_close_window ()</l>
<l>read_image (Angio, 'angio-part')</l>
<l>get_image_size (Angio, Width, Height)</l>
<l>dev_open_window (0, 0, 3 * Width / 2, 3 * Height / 2, 'black', WindowID)</l>
<l>dev_display (Angio)</l>
<l>dev_set_color ('blue')</l>
<l>MaxLineWidth := 8</l>
<l>Contrast := 12</l>
<l>calculate_lines_gauss_parameters (MaxLineWidth, [Contrast,0], Sigma, Low, High)</l>
<l>lines_gauss (Angio, Lines, Sigma, Low, High, 'dark', 'true', 'parabolic', 'true')</l>
<l>count_obj (Lines, Number)</l>
<l>dev_update_pc ('off')</l>
<l>dev_update_var ('off')</l>
<l>for I := 1 to Number by 1</l>
<l>    select_obj (Lines, Line, I)</l>
<l>    get_contour_xld (Line, Row, Col)</l>
<l>    get_contour_attrib_xld (Line, 'angle', Angle)</l>
<l>    get_contour_attrib_xld (Line, 'width_left', WidthL)</l>
<l>    get_contour_attrib_xld (Line, 'width_right', WidthR)</l>
<c>    * To display the lines, the point at which the gray value drops to</c>
<c>    * 25% of the contrast between the line and the background will be</c>
<c>    * displayed.  This point is given by sqrt(3/4) for the parabolic</c>
<c>    * line model.</c>
<l>    RowR := Row + cos(Angle) * WidthR * sqrt(0.75)</l>
<l>    ColR := Col + sin(Angle) * WidthR * sqrt(0.75)</l>
<l>    RowL := Row - cos(Angle) * WidthL * sqrt(0.75)</l>
<l>    ColL := Col - sin(Angle) * WidthL * sqrt(0.75)</l>
<l>    dev_set_color ('red')</l>
<l>    dev_display (Line)</l>
<l>    dev_set_color ('green')</l>
<l>    disp_polygon (WindowID, RowL, ColL)</l>
<l>    disp_polygon (WindowID, RowR, ColR)</l>
<l>endfor</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
