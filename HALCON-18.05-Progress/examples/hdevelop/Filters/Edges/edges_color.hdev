<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="18.05">
<procedure name="main">
<interface/>
<body>
<c>* This example program shows how to use edges_color and the differences</c>
<c>* between the output of edges_color and edges_image using an image in which</c>
<c>* some edges can only be extracted from a color image.</c>
<l>dev_update_off ()</l>
<l>read_image (Image, 'olympic_stadium')</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width, Height, 'black', WindowHandle)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_display (Image)</l>
<l>Message := 'Color image'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>stop ()</l>
<c>* Convert the color image to a gray value image.  Note that there is no contrast</c>
<c>* between the soccer field and the track in the gray value image.</c>
<l>rgb1_to_gray (Image, GrayImage)</l>
<l>dev_display (GrayImage)</l>
<l>Message := 'Gray value image'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>stop ()</l>
<c>* Compute the color edges without non-maximum suppression and hysteresis</c>
<c>* thresholding, i.e., the raw edge amplitude image.  Note that there is a clearly</c>
<c>* visible edge between the soccer field and the track in the color edge image.</c>
<l>edges_color (Image, ImaAmp, ImaDir, 'canny', 1, 'none', -1, -1)</l>
<l>dev_display (ImaAmp)</l>
<l>Message := 'Color edge image'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>stop ()</l>
<c>* Compute the gray value edges without non-maximum suppression and</c>
<c>* hysteresis thresholding, i.e., the raw edge amplitude image.  Note that there is</c>
<c>* no visible edge between the soccer field and the track in the gray value edge</c>
<c>* image.</c>
<l>edges_image (GrayImage, ImaAmpGray, ImaDirGray, 'canny', 1, 'none', -1, -1)</l>
<l>dev_display (ImaAmpGray)</l>
<l>Message := 'Gray value edge image'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>stop ()</l>
<c>* Now perform the color edge extraction including non-maximum suppression</c>
<c>* and hysteresis thresholding.  Note the segmented edge between the soccer</c>
<c>* field and the track.</c>
<l>edges_color (Image, ImaAmpHyst, ImaDirHyst, 'canny', 1, 'nms', 20, 40)</l>
<l>threshold (ImaAmpHyst, RegionColor, 1, 255)</l>
<l>skeleton (RegionColor, EdgesColor)</l>
<l>dev_display (Image)</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (EdgesColor)</l>
<l>Message := 'Segmented color edges'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>stop ()</l>
<c>* Now perform the gray value edge extraction including non-maximum</c>
<c>* suppression and hysteresis thresholding.  Note the absence of an edge</c>
<c>* between the soccer field and the track.</c>
<l>edges_image (GrayImage, ImaAmpGrayHyst, ImaDirGrayHyst, 'canny', 1, 'nms', 20, 40)</l>
<l>threshold (ImaAmpGrayHyst, RegionGray, 1, 255)</l>
<l>skeleton (RegionGray, EdgesGray)</l>
<l>dev_display (GrayImage)</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (EdgesGray)</l>
<l>Message := 'Segmented gray value edges'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>stop ()</l>
<c>* Now display everything again, while only showing the part of the image</c>
<c>* containing the soccer field and the track.</c>
<l>Row1 := 220</l>
<l>Col1 := 215</l>
<l>Row2 := 399</l>
<l>Col2 := 489</l>
<l>dev_set_part (Row1, Col1, Row2, Col2)</l>
<l>dev_display (Image)</l>
<l>stop ()</l>
<l>dev_display (GrayImage)</l>
<l>stop ()</l>
<l>dev_display (ImaAmp)</l>
<l>stop ()</l>
<l>dev_display (ImaAmpGray)</l>
<l>stop ()</l>
<l>dev_display (Image)</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (EdgesColor)</l>
<l>stop ()</l>
<l>dev_display (GrayImage)</l>
<l>dev_set_color ('blue')</l>
<l>dev_display (EdgesGray)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
