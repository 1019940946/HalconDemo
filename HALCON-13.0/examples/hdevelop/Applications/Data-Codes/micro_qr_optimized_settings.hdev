<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<c>* *****************************************************************</c>
<c>* This example program demonstrates how to read Micro QR codes</c>
<c>* with manually adapted parameter settings to optimize the</c>
<c>* recognition and runtime.</c>
<c>* </c>
<c>* The following parameters are set manually:</c>
<c>*    - 'module_size_min'</c>
<c>*    - 'module_size_max'</c>
<c>*    - 'module_gap'</c>
<c>*    - 'polarity'</c>
<c>*    - 'mirrored'</c>
<c>*    - 'contrast_min'</c>
<c>*    - 'symbol_size_min'</c>
<c>*    - 'symbol_size_max'</c>
<c>* </c>
<c>* In particular, the setting of the polarity - if it is</c>
<c>* 'light_on_dark' - helps to reduce the runtime.</c>
<c>* For dark-on-light symbols only a low improvement of the runtime</c>
<c>* can be achieved. However, if the symbol is found at big images or</c>
<c>* if the symbol is not readable, the time saving may be significant.</c>
<c>* ******************************************************************</c>
<c>* </c>
<c>* Initialize image path and visual settings</c>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>list_image_files ('datacode/micro_qr', 'default', [], ImageFiles)</l>
<l>ImageFiles := regexp_select(ImageFiles,'(cell_phone)|(board)')</l>
<l>read_image (Image, ImageFiles[0])</l>
<l>dev_open_window_fit_image (Image, 0, 0, -1, -1, WindowHandle)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>dev_set_line_width (3)</l>
<l>dev_set_color ('green')</l>
<c>* </c>
<c>* Display short description</c>
<l>Message := 'This program demonstrates how to read'</l>
<l>Message[1] := 'Micro QR data codes with manually adapted'</l>
<l>Message[2] := 'parameter settings.'</l>
<l>Message[3] := ' '</l>
<l>Message[4] := 'The following parameters are adapted to'</l>
<l>Message[5] := 'optimize the recognition and runtime:'</l>
<l>Message[6] := ' '</l>
<l>Message[7] := '  - \'module_size_min\''</l>
<l>Message[8] := '  - \'module_size_max\''</l>
<l>Message[9] := '  - \'module_gap\''</l>
<l>Message[10] := '  - \'polarity\''</l>
<l>Message[11] := '  - \'mirrored\''</l>
<l>Message[12] := '  - \'contrast_min\''</l>
<l>Message[13] := '  - \'symbol_size_min\''</l>
<l>Message[14] := '  - \'symbol_size_max\''</l>
<l>Message[15] := '  - \'small_modules_robustness\''</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* </c>
<c>* Step 1: Create a 2d data code model</c>
<c>* ----------------------------------------------------</c>
<c>* Create a 2d data code model of the 2d data code class</c>
<c>* 'Data Matrix ECC 200'. The operator returns a handle to</c>
<c>* the 2d data code model which can be used for all further</c>
<c>* operations on the data code.</c>
<l>create_data_code_2d_model ('Micro QR Code', [], [], DataCodeHandle)</l>
<c>* </c>
<c>* Step 2: Adapt some parameters manually</c>
<c>* ----------------------------------------------------</c>
<c>* Set some selected parameters of the 2d data code model</c>
<c>* in order to adapt the model to the symbol appearance.</c>
<l>set_data_code_2d_param (DataCodeHandle, ['module_size_min','module_size_max'], [3,8])</l>
<l>set_data_code_2d_param (DataCodeHandle, 'module_gap', 'no')</l>
<l>set_data_code_2d_param (DataCodeHandle, 'polarity', 'any')</l>
<l>set_data_code_2d_param (DataCodeHandle, 'mirrored', 'no')</l>
<l>set_data_code_2d_param (DataCodeHandle, 'contrast_min', 20)</l>
<l>set_data_code_2d_param (DataCodeHandle, 'symbol_size_min', 13)</l>
<l>set_data_code_2d_param (DataCodeHandle, 'symbol_size_max', 15)</l>
<l>set_data_code_2d_param (DataCodeHandle, 'small_modules_robustness', 'high')</l>
<c>* </c>
<c>* Step 3: Read the data codes</c>
<c>* ----------------------------------------------------</c>
<c>* Search and read the data codes in each image and</c>
<c>* display the decoded string for each found data code</c>
<l>for Index := 0 to |ImageFiles| - 1 by 1</l>
<l>    read_image (Image, ImageFiles[Index])</l>
<l>    dev_display (Image)</l>
<c>    * </c>
<c>    * Find and decode the data codes and measure the runtime</c>
<l>    count_seconds (T1)</l>
<l>    find_data_code_2d (Image, SymbolXLDs, DataCodeHandle, [], [], ResultHandles, DecodedDataStrings)</l>
<l>    count_seconds (T2)</l>
<l>    Time := 1000 * (T2 - T1)</l>
<c>    * </c>
<c>    * Display the results</c>
<l>    TitleMessage := 'Image ' + (Index + 1) + ' of ' + |ImageFiles|</l>
<l>    ResultMessage := 'Data code found in ' + Time$'.1f' + ' ms'</l>
<l>    display_found_data_codes (SymbolXLDs, WindowHandle, DecodedDataStrings, TitleMessage, ResultMessage, 'forest green', 'black')</l>
<c>    * </c>
<c>    * Deactivate the following lines to run the program without breaks</c>
<l>    if (Index &lt; |ImageFiles| - 1)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
<c>* </c>
<c>* Clear the 2d data code model</c>
<l>clear_data_code_2d_model (DataCodeHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
<procedure name="display_found_data_codes">
<interface>
<io>
<par name="SymbolXLDs" base_type="iconic" dimension="0"/>
</io>
<ic>
<par name="WindowHandle" base_type="ctrl" dimension="0"/>
<par name="DecodedDataStrings" base_type="ctrl" dimension="0"/>
<par name="TitleMessage" base_type="ctrl" dimension="0"/>
<par name="ResultMessage" base_type="ctrl" dimension="0"/>
<par name="ColorDecodedStrings" base_type="ctrl" dimension="0"/>
<par name="ColorResult" base_type="ctrl" dimension="0"/>
</ic>
</interface>
<body>
<c>* This procedure displays the results of the search for</c>
<c>* 2d data codes. The data strings are displayed accordingly</c>
<c>* to their length so that the whole string is visible.</c>
<c>* If the data strings are too long only the first 50 chars</c>
<c>* are displayed.</c>
<c>* </c>
<c>* Input parameters are the XLD contours of the decoded</c>
<c>* data symbols, the decoded data strings, the windowhandle,</c>
<c>* a title message, a result message, the color of the decoded</c>
<c>* strings and the color of the result message.</c>
<c>* </c>
<c>* Display the result of the search for each found data code</c>
<l>for J := 0 to |DecodedDataStrings| - 1 by 1</l>
<c>    * </c>
<c>    * Display the XLD contour</c>
<l>    select_obj (SymbolXLDs, SymbolXLD, J + 1)</l>
<l>    get_contour_xld (SymbolXLD, Row, Column)</l>
<l>    dev_display (SymbolXLD)</l>
<c>    * </c>
<c>    * Display messages</c>
<c>    * ------------------</c>
<c>    * Determine the length of the dislayed decoded data string</c>
<l>    get_window_extents (WindowHandle, Row1, Column1, Width, Height)</l>
<l>    get_string_extents (WindowHandle, DecodedDataStrings[J], Ascent, Descent, TWidth, THeight)</l>
<l>    if (TWidth &gt; Width)</l>
<l>        DecodedDataStrings[J] := DecodedDataStrings[J]{0:50} + '...'</l>
<l>        get_string_extents (WindowHandle, DecodedDataStrings[J], Ascent, Descent, TWidth, THeight)</l>
<l>    endif</l>
<c>    * </c>
<c>    * Split the decoded string in new lines for better readability</c>
<l>    tuple_regexp_replace (DecodedDataStrings[J], ['[\\r\\f,^#;]','replace_all'], '\n', DecodedData)</l>
<l>    tuple_split (DecodedData, '\n', DecodedDataSubstrings)</l>
<c>    * </c>
<c>    * Determine the position of the displayed decoded data string</c>
<l>    if (max(Row) &gt; 420 and min(Row) &lt; 40)</l>
<l>        TPosRow := max(Row) - 30</l>
<l>    elseif (max(Row) &gt; 420)</l>
<l>        TPosRow := min(Row) - 20</l>
<l>    elseif (min(Row) &lt; 100)</l>
<l>        TPosRow := max(Row) - 20</l>
<l>    else</l>
<l>        TPosRow := max(Row) - 30</l>
<l>    endif</l>
<l>    TPosColumn := max([min([mean(Column) - TWidth / 2,Width - 32 - TWidth]),12])</l>
<l>    disp_message (WindowHandle, DecodedDataStrings[J], 'image', TPosRow, TPosColumn, ColorDecodedStrings, 'true')</l>
<l>endfor</l>
<c>* </c>
<c>* Display the title message and result message</c>
<l>disp_message (WindowHandle, TitleMessage, 'window', 12, 12, 'black', 'true')</l>
<l>disp_message (WindowHandle, ResultMessage, 'window', 40, 12, ColorResult, 'true')</l>
<l>return ()</l>
</body>
<docu id="display_found_data_codes">
<parameters>
<parameter id="ColorDecodedStrings"/>
<parameter id="ColorResult"/>
<parameter id="DecodedDataStrings"/>
<parameter id="ResultMessage"/>
<parameter id="SymbolXLDs"/>
<parameter id="TitleMessage"/>
<parameter id="WindowHandle"/>
</parameters>
</docu>
</procedure>
</hdevelop>
