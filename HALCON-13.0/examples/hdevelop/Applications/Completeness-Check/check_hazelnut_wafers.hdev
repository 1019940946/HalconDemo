<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<c>* This example demonstrates a quality inspection on hazelnut wavers.</c>
<c>* Using the morphology tools the waver is extracted and examined</c>
<c>* according to a few shape features like Rectangularity and AreaHoles.</c>
<c>* This program also shows the use of the operator area_holes.</c>
<c>* </c>
<l>read_image (Image, 'food/hazelnut_wafer_01')</l>
<l>dev_close_window ()</l>
<l>dev_open_window_fit_image (Image, 0, 0, -1, -1, WindowHandle)</l>
<l>dev_update_window ('off')</l>
<l>dev_set_line_width (3)</l>
<l>dev_set_draw ('margin')</l>
<l>set_display_font (WindowHandle, 20, 'mono', 'true', 'false')</l>
<c>* </c>
<l>for Index := 1 to 24 by 1</l>
<l>    read_image (Image, 'food/hazelnut_wafer_' + Index$'.02')</l>
<l>    binary_threshold (Image, Foreground, 'smooth_histo', 'light', UsedThreshold)</l>
<l>    opening_circle (Foreground, FinalRegion, 8.5)</l>
<l>    area_holes (FinalRegion, AreaHoles)</l>
<l>    rectangularity (FinalRegion, Rectangularity)</l>
<l>    dev_display (Image)</l>
<l>    if (AreaHoles &gt; 300 or Rectangularity &lt; 0.92)</l>
<l>        dev_set_color ('red')</l>
<l>        Text := 'Not OK'</l>
<l>    else</l>
<l>        dev_set_color ('forest green')</l>
<l>        Text := 'OK'</l>
<l>    endif</l>
<l>    dev_display (FinalRegion)</l>
<l>    disp_message (WindowHandle, Text, 'window', 12, 12, '', 'false')</l>
<l>    if (Index &lt; 24)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
