<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<l>read_image (Image, 'fonts/arial_a1')</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width / 2, Height / 2 + 42, 'black', WindowHandle)</l>
<l>dev_set_part (-84, 0, Height - 1, Width - 1)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>dev_display (Image)</l>
<l>dev_set_draw ('margin')</l>
<l>gen_rectangle1 (Rectangle, 37, 69, 115, 141)</l>
<l>reduce_domain (Image, Rectangle, ImageReduced)</l>
<l>create_ocv_proj ('A', OCVHandle)</l>
<l>traind_ocv_proj (ImageReduced, OCVHandle, 'A', 'single')</l>
<l>dev_set_color ('red')</l>
<l>for I := 1 to 9 by 1</l>
<l>    read_image (Image, 'fonts/arial_a' + I)</l>
<l>    binary_threshold (Image, Region, 'max_separability', 'dark', UsedThreshold)</l>
<l>    connection (Region, ConnectedRegions)</l>
<l>    select_shape (ConnectedRegions, SelectedRegions, 'area', 'and', 150, 99999)</l>
<l>    sort_region (SelectedRegions, SortedRegions, 'character', 'true', 'row')</l>
<l>    select_obj (SortedRegions, ObjectSelected, 1)</l>
<l>    shape_trans (ObjectSelected, RegionTrans, 'rectangle1')</l>
<l>    dilation_rectangle1 (RegionTrans, RegionDilation, 15, 15)</l>
<l>    reduce_domain (Image, RegionDilation, ImageReduced)</l>
<l>    do_ocv_simple (ImageReduced, OCVHandle, 'A', 'true', 'true', 'true', 'true', 5, Quality)</l>
<c>    * Display quality</c>
<l>    if (Quality &gt; 0.9)</l>
<l>        Color := 'green'</l>
<l>    elseif (Quality &gt; 0.7)</l>
<l>        Color := 'yellow'</l>
<l>    else</l>
<l>        Color := 'red'</l>
<l>    endif</l>
<l>    dev_display (Image)</l>
<l>    dev_set_color (Color)</l>
<l>    dev_set_line_width (2)</l>
<l>    dev_display (RegionDilation)</l>
<l>    disp_message (WindowHandle, 'Check print quality of \'A\' (Image  ' + I + ' of 9)', 'window', 12, 12, 'black', 'true')</l>
<l>    disp_message (WindowHandle, 'Quality = ' + Quality$'.2f', 'image', 120, 12, 'black', Color)</l>
<l>    if (I &lt; 9)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
<l>stop ()</l>
<l>close_ocv (OCVHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
