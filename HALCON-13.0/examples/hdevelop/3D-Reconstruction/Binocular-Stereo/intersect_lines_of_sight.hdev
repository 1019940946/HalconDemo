<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<c>* Example program to illustrate the operator 'intersect_lines_of_sight'</c>
<c>* </c>
<c>* The following algorithm is performed for an original and a rectified binocular camera system:</c>
<c>* 1) Two cameras are defined;</c>
<c>* 2) One (virtual) calibration plate is projected into both cameras;</c>
<c>* 3) The circular marks of the calibration plate are extracted in both images of the plate;</c>
<c>* 4) The 3D points of the calibration plate are reconstructed and</c>
<c>*     the results are compared with the 3D model points of the plate</c>
<c>* -------------------------------------------------------------------</c>
<c>* (1) Define the relative pose of camera 2 with respect to camera 1</c>
<c>* (this is the pose as it would be returned by binocular calibration)</c>
<l>roty := 15</l>
<l>base := 0.08</l>
<l>create_pose (base * cos(rad(roty)), 0, base * sin(rad(roty)), 0, -2 * roty, 0, 'Rp+T', 'gba', 'point', c1Pc2_model)</l>
<l>pose_to_hom_mat3d (c1Pc2_model, c1Tc2_model)</l>
<c>* </c>
<c>* Define the internal parameters of the cameras</c>
<l>Width := 320</l>
<l>Height := 240</l>
<l>gen_cam_par_area_scan_division (0.0085, -1000.0, 7.4e-6, 7.4e-6, Width / 2, Height / 2, Width, Height, AVTCam1)</l>
<l>AVTCam2 := AVTCam1</l>
<c>* </c>
<c>* Prepare the visualization</c>
<l>dev_update_window ('off')</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width, Height, 'black', WindowHandle1)</l>
<l>set_display_font (WindowHandle1, 14, 'mono', 'true', 'false')</l>
<l>dev_open_window (0, Width + 12, Width, Height, 'black', WindowHandle2)</l>
<c>* </c>
<c>* (2) Define the pose of the calibration plate (with respect to camera1)</c>
<l>dist := 0.15</l>
<l>create_pose (base / 2.0 * cos(rad(roty)) - dist * sin(rad(roty)), 0, base / 2.0 * sin(rad(roty)) + dist * cos(rad(roty)), 0, -roty, 0, 'Rp+T', 'gba', 'point', c1Ptab)</l>
<l>pose_to_hom_mat3d (c1Ptab, c1Ttab)</l>
<l>pose_invert (c1Pc2_model, c2Pc1_model)</l>
<l>pose_compose (c2Pc1_model, c1Ptab, c2Ptab)</l>
<c>* </c>
<c>* Generate the images of the simulated calibration plates ...</c>
<l>sim_caltab (TabImage1, 'caltab_30mm.descr', AVTCam1, c1Ptab, 128, 224, 80, 0.5)</l>
<l>sim_caltab (TabImage2, 'caltab_30mm.descr', AVTCam2, c2Ptab, 128, 224, 80, 0.5)</l>
<c>* ... and visualize the images:</c>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_display (TabImage1)</l>
<l>dev_set_window (WindowHandle2)</l>
<l>dev_display (TabImage2)</l>
<c>* </c>
<c>* (3) Find the points and their correspondences in the images</c>
<l>find_caltab (TabImage1, Caltab, 'caltab_30mm.descr', 3, 112, 5)</l>
<l>find_marks_and_pose (TabImage1, Caltab, 'caltab_30mm.descr', AVTCam1, 128, 10, 18, 0.9, 15, 100, RCoord1, CCoord1, StartPose1)</l>
<l>find_caltab (TabImage2, Caltab, 'caltab_30mm.descr', 3, 112, 5)</l>
<l>find_marks_and_pose (TabImage2, Caltab, 'caltab_30mm.descr', AVTCam2, 128, 10, 18, 0.9, 15, 100, RCoord2, CCoord2, StartPose2)</l>
<l>gen_cross_contour_xld (Cross1, RCoord1, CCoord1, 6, 0)</l>
<l>gen_cross_contour_xld (Cross2, RCoord2, CCoord2, 6, 0)</l>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_set_color ('yellow')</l>
<l>dev_display (Cross1)</l>
<l>dev_set_window (WindowHandle2)</l>
<l>dev_set_color ('cyan')</l>
<l>dev_display (Cross2)</l>
<c>* </c>
<c>* (4) Compute the 3d points of the calibration plate by intersect_lines_of_sight</c>
<l>intersect_lines_of_sight (AVTCam1, AVTCam2, c1Pc2_model, RCoord1, CCoord1, RCoord2, CCoord2, X, Y, Z, Dist)</l>
<c>* </c>
<c>* Transform the 3d caltab points into coordinate system of camera 1</c>
<l>caltab_points ('caltab_30mm.descr', CalX, CalY, CalZ)</l>
<l>affine_trans_point_3d (c1Ttab, CalX, CalY, CalZ, c1CalX, c1CalY, c1CalZ)</l>
<c>* </c>
<c>* Compare the results (reconstructed points to real 3D points)</c>
<l>tuple_sub (X, c1CalX, XDiff)</l>
<l>tuple_sub (Y, c1CalY, YDiff)</l>
<l>tuple_sub (Z, c1CalZ, ZDiff)</l>
<c>* Note: The values of XDiff,YDiff,ZDiff should be very small  (accuracy at some um (=e-6 m))</c>
<l>disp_continue_message (WindowHandle1, 'black', 'true')</l>
<l>stop ()</l>
<c>* </c>
<c>* -----------------------------------------------------------</c>
<c>* Do the same with rectified images ...</c>
<c>* </c>
<c>* (1)+(2) Rectify the images of the plate</c>
<l>gen_binocular_rectification_map (Map1, Map2, AVTCam1, AVTCam2, c1Pc2_model, 1, 'geometric', 'bilinear', CamParamRect1, CamParamRect2, CamPoseRect1, CamPoseRect2, RelPoseRect)</l>
<l>map_image (TabImage1, Map1, ImageMapped1)</l>
<l>map_image (TabImage2, Map2, ImageMapped2)</l>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_clear_window ()</l>
<l>dev_display (ImageMapped1)</l>
<l>dev_set_window (WindowHandle2)</l>
<l>dev_clear_window ()</l>
<l>dev_display (ImageMapped2)</l>
<c>* </c>
<c>* (3) Find the points and their correspondences in the images</c>
<l>find_caltab (ImageMapped1, Caltab, 'caltab_30mm.descr', 3, 112, 5)</l>
<l>find_marks_and_pose (ImageMapped1, Caltab, 'caltab_30mm.descr', CamParamRect1, 128, 10, 18, 0.9, 15, 100, RCoord1, CCoord1, StartPose1)</l>
<l>find_caltab (ImageMapped2, Caltab, 'caltab_30mm.descr', 3, 112, 5)</l>
<l>find_marks_and_pose (ImageMapped2, Caltab, 'caltab_30mm.descr', CamParamRect2, 128, 10, 18, 0.9, 15, 100, RCoord2, CCoord2, StartPose2)</l>
<l>gen_cross_contour_xld (Cross1, RCoord1, CCoord1, 6, 0)</l>
<l>gen_cross_contour_xld (Cross2, RCoord2, CCoord2, 6, 0)</l>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_set_color ('yellow')</l>
<l>dev_display (Cross1)</l>
<l>dev_set_window (WindowHandle2)</l>
<l>dev_set_color ('cyan')</l>
<l>dev_display (Cross2)</l>
<c>* </c>
<c>* (4) Compute the 3d points of the calibration plate by intersect_lines_of_sight</c>
<l>intersect_lines_of_sight (CamParamRect1, CamParamRect2, RelPoseRect, RCoord1, CCoord1, RCoord2, CCoord2, X, Y, Z, Dist)</l>
<c>* </c>
<c>* !!! Transform the reconstructed caltab points from the rectified coordinate system of camera 1</c>
<c>*     into the *original* coordinate system of camera 1</c>
<l>pose_to_hom_mat3d (CamPoseRect1, c1Trect1)</l>
<l>affine_trans_point_3d (c1Trect1, X, Y, Z, c1X, c1Y, c1Z)</l>
<c>* </c>
<c>* Compare the results (reconstructed points with real 3D points)</c>
<l>tuple_sub (c1X, c1CalX, XDiff)</l>
<l>tuple_sub (c1Y, c1CalY, YDiff)</l>
<l>tuple_sub (c1Z, c1CalZ, ZDiff)</l>
<c>* Note: the values of XDiff,YDiff,ZDiff should be very small  (accuracy at some um (=e-6 m))</c>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
