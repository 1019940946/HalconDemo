<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<c>* This example program shows how to use binocular_disparity_mg to</c>
<c>* determine the disparities of the components on a PCB with different</c>
<c>* levels of accuracy.</c>
<l>dev_update_window ('off')</l>
<l>dev_update_var ('off')</l>
<l>dev_update_pc ('off')</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 672, 484, 'black', WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>read_image (ImageL, 'stereo/board/board_l_01')</l>
<l>read_image (ImageR, 'stereo/board/board_r_01')</l>
<c>* The following camera and relative pose parameters were obtained with</c>
<c>* binocular_calibration.</c>
<l>gen_cam_par_area_scan_division (0.0130507774353, -665.817817207, 1.4803417027e-5, 1.48e-5, 155.89225769, 126.70664978, 320, 240, CamParamL)</l>
<l>gen_cam_par_area_scan_division (0.0131776504517, -731.860636733, 1.47997569293e-5, 1.48e-5, 162.98210144, 119.301040649, 320, 240, CamParamR)</l>
<l>RelPose := [0.153573364258,-0.00373362231255,0.0447351264954,0.174289124775,319.843388114,359.894955219,0]</l>
<c>* Create the rectification maps from the calibration data.</c>
<l>gen_binocular_rectification_map (MapL, MapR, CamParamL, CamParamR, RelPose, 1, 'geometric', 'bilinear', RectCamParL, RectCamParR, CamPoseRectL, CamPoseRectR, RectLPosRectR)</l>
<c>* Rectify the images.</c>
<l>map_image (ImageL, MapL, ImageRectifiedL)</l>
<l>map_image (ImageR, MapR, ImageRectifiedR)</l>
<l>dev_clear_window ()</l>
<l>dev_display (ImageRectifiedL)</l>
<l>disp_message (WindowHandle, 'Left stereo image', 'window', 0, 0, 'black', 'true')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* Loop over all default parameter sets of binocular_disparity_mg.</c>
<l>DefaultParameters := ['fast','fast_accurate','accurate','very_accurate']</l>
<l>for I := 0 to |DefaultParameters| - 1 by 1</l>
<l>    Parameters := DefaultParameters[I]</l>
<c>    * Compute the disparities.</c>
<l>    count_seconds (S1)</l>
<l>    binocular_disparity_mg (ImageRectifiedL, ImageRectifiedR, Disparity, Score, 1, 10, 5, 0, 'false', 'default_parameters', Parameters)</l>
<l>    count_seconds (S2)</l>
<l>    dev_clear_window ()</l>
<l>    dev_display (Disparity)</l>
<l>    disp_message (WindowHandle, 'Default parameters: ' + Parameters + ', ' + 'time: ' + (S2 - S1)$'5.3f' + 's', 'window', 0, 0, 'black', 'true')</l>
<l>    if (I &lt; |DefaultParameters| - 1)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>    endif</l>
<l>    stop ()</l>
<l>endfor</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
