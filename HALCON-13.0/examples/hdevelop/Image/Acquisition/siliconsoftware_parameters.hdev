<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<c>* </c>
<c>* Example for the usage of the HALCON SiliconSoftware Interface.</c>
<c>* </c>
<c>* </c>
<c>* </c>
<l>info_framegrabber ('SiliconSoftware', 'camera_type', CameraTypeInfo, CameraTypeInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'defaults', DefaultInfo, DefaultInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'device', DeviceInfo, DeviceInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'general', GeneralInfo, GeneralInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'info_boards', BoardInfo, BoardInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'port', PortInfo, PortInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'parameters', ParameterInfo, ParameterInfoValues)</l>
<l>info_framegrabber ('SiliconSoftware', 'revision', RevisionInfo, RevisionInfoValues)</l>
<c>* </c>
<l>dev_update_pc ('off')</l>
<c>* Note: You have to replace the values of the parameters MyCamType and MyPort with</c>
<c>* the full file name of your specific camera configuration file and the actually connected port!</c>
<l>MyCamType := 'MyCam.mcf'</l>
<l>MyPort := 0</l>
<c>* </c>
<l>open_framegrabber ('SiliconSoftware', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'false', MyCamType, 'default', MyPort, -1, AcqHandle)</l>
<c>* </c>
<l>get_system ('do_low_error', LowErrorFlag)</l>
<l>if (LowErrorFlag == 'true')</l>
<l>    set_system ('do_low_error', 'false')</l>
<l>endif</l>
<c>* query all available parameter names and sort them:</c>
<l>info_framegrabber ('SiliconSoftware', 'parameters', ParameterInfo, ParameterInfoValues)</l>
<l>tuple_sort (ParameterInfoValues, ParameterInfoValues)</l>
<c>* </c>
<c>* open graphics window for parameter output:</c>
<l>dev_close_window ()</l>
<l>ImageHeight := 12 * |ParameterInfoValues| + 20</l>
<l>dev_open_window (0, 0, 400, ImageHeight, 'black', WindowHandle)</l>
<l>dev_set_part (0, 0, ImageHeight - 1, 399)</l>
<c>* </c>
<c>* query all current parameter values:</c>
<l>dev_set_check ('~give_error')</l>
<l>dev_error_var (HalconError, 1)</l>
<l>for i := 0 to |ParameterInfoValues| - 1 by 1</l>
<l>    dev_set_color ('white')</l>
<l>    set_tposition (WindowHandle, (12 * i) + 2, 10)</l>
<l>    write_string (WindowHandle, ParameterInfoValues[i] + ': ')</l>
<l>    get_framegrabber_param (AcqHandle, ParameterInfoValues[i], ParameterValue)</l>
<l>    if (HalconError == H_MSG_TRUE)</l>
<l>        dev_set_color ('green')</l>
<l>        if (|ParameterValue| &gt; 1)</l>
<l>            for j := 0 to |ParameterValue| - 2 by 1</l>
<l>                write_string (WindowHandle, ParameterValue[j] + ',')</l>
<l>            endfor</l>
<l>            write_string (WindowHandle, ParameterValue[j])</l>
<l>        else</l>
<l>            if (|ParameterValue| == 1)</l>
<l>                write_string (WindowHandle, ParameterValue)</l>
<l>            else</l>
<l>                write_string (WindowHandle, '-')</l>
<l>            endif</l>
<l>        endif</l>
<l>    else</l>
<l>        dev_set_color ('red')</l>
<l>        write_string (WindowHandle, '&lt;not supported&gt;')</l>
<l>    endif</l>
<l>endfor</l>
<l>dev_set_check ('give_error')</l>
<l>set_system ('do_low_error', LowErrorFlag)</l>
<l>dev_update_pc ('on')</l>
<l>stop ()</l>
<c>* </c>
<l>grab_image (Image, AcqHandle)</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, Width, Height, 'black', WindowID)</l>
<l>dev_display (Image)</l>
<c>* </c>
<l>for i := 1 to 100 by 1</l>
<l>    grab_image_async (Image, AcqHandle, -1)</l>
<l>endfor</l>
<c>* </c>
<l>close_framegrabber (AcqHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
