<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="13.0">
<procedure name="main">
<interface/>
<body>
<c>* </c>
<c>* Example for grabbing images from two VRmagic USB cameras</c>
<c>* (concurrent grabbing from two connected cameras)</c>
<c>* </c>
<c>* note that using the 'image_available' parameter allows us</c>
<c>* to grab from devices at different framerates in a single loop!</c>
<c>* </c>
<l>dev_update_off ()</l>
<c>* Check interface revision:</c>
<l>info_framegrabber ('VRmUsbCam', 'revision', RevInfo, RevInfoValues)</l>
<c>* </c>
<c>* Check your installed cameras for device and port:</c>
<l>info_framegrabber ('VRmUsbCam', 'devices', BoardInfo, BoardInfoValues)</l>
<c>* </c>
<c>* Open the two cameras using their device names or indices , e.g.:</c>
<l>MyDevice1 := '0'</l>
<l>MyDevice2 := '1'</l>
<l>MyCameraType := 'default'</l>
<l>open_framegrabber ('VRmUsbCam', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'false', MyCameraType, MyDevice1, -1, -1, AcqHandle1)</l>
<l>open_framegrabber ('VRmUsbCam', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'false', MyCameraType, MyDevice2, -1, -1, AcqHandle2)</l>
<c>* </c>
<l>dev_close_window ()</l>
<l>dev_close_window ()</l>
<l>get_framegrabber_param (AcqHandle1, ['image_width','image_height'], ImageSize1)</l>
<l>dev_open_window (0, 0, ImageSize1[0] / 2, ImageSize1[1] / 2, 'black', WindowHandle1)</l>
<l>dev_set_part (0, 0, ImageSize1[1] - 1, ImageSize1[0] - 1)</l>
<l>get_framegrabber_param (AcqHandle2, ['image_width','image_height'], ImageSize2)</l>
<l>dev_open_window (0, (ImageSize1[0] / 2) + 10, ImageSize2[0] / 2, ImageSize2[1] / 2, 'black', WindowHandle2)</l>
<l>dev_set_part (0, 0, ImageSize2[1] - 1, ImageSize2[0] - 1)</l>
<c>* </c>
<l>get_framegrabber_param (AcqHandle1, 'grab_framerate_estimated_f', EstFPS1)</l>
<l>get_framegrabber_param (AcqHandle2, 'grab_framerate_estimated_f', EstFPS2)</l>
<c>* </c>
<l>NumImages := 0</l>
<l>grab_image_start (AcqHandle1, -1)</l>
<l>grab_image_start (AcqHandle2, -1)</l>
<l>count_seconds (SecondsBegin)</l>
<l>while (1)</l>
<l>    get_framegrabber_param (AcqHandle1, 'image_available', ImageReady1)</l>
<l>    if (ImageReady1)</l>
<l>        grab_image_async (Image1, AcqHandle1, -1)</l>
<l>        dev_set_window (WindowHandle1)</l>
<l>        dev_display (Image1)</l>
<l>        NumImages := NumImages + 1</l>
<l>    endif</l>
<l>    get_framegrabber_param (AcqHandle2, 'image_available', ImageReady2)</l>
<l>    if (ImageReady2)</l>
<l>        grab_image_async (Image2, AcqHandle2, -1)</l>
<l>        dev_set_window (WindowHandle2)</l>
<l>        dev_display (Image2)</l>
<l>        NumImages := NumImages + 1</l>
<l>    endif</l>
<l>    count_seconds (SecondsCurrent)</l>
<l>    FrameRate := NumImages / (SecondsCurrent - SecondsBegin)</l>
<l>endwhile</l>
<c>* </c>
<l>close_framegrabber (AcqHandle1)</l>
<l>close_framegrabber (AcqHandle2)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
