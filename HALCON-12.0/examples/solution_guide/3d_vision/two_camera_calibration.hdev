<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* Example program for high precision mosaicking based on camera calibration</c>
<l>dev_update_off ()</l>
<l>ImgPath := '3d_machine_vision/multiple_cameras/'</l>
<c>* </c>
<c>* ================================================</c>
<c>* &gt;&gt;&gt;   Window of left image and right image   &lt;&lt;&lt;</c>
<c>* ================================================</c>
<l>dev_close_window ()</l>
<l>read_image (Image1, ImgPath + 'camera1_ref')</l>
<l>get_image_size (Image1, Width, Height)</l>
<l>WindowScale := 0.66</l>
<l>dev_open_window (0, 0, Width * WindowScale, Height * WindowScale, 'black', WindowHandle1)</l>
<l>dev_set_color ('green')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (2)</l>
<l>dev_open_window (0, Width * WindowScale + 6, Width * WindowScale, Height * WindowScale, 'black', WindowHandle2)</l>
<l>dev_set_color ('green')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (2)</l>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>set_display_font (WindowHandle1, 16, 'mono', 'true', 'false')</l>
<l>dev_set_window (WindowHandle2)</l>
<l>dev_set_part (0, 0, Height - 1, Width - 1)</l>
<l>set_display_font (WindowHandle2, 16, 'mono', 'true', 'false')</l>
<c>* </c>
<c>* </c>
<c>* </c>
<c>* =======================</c>
<c>* &gt;&gt;&gt;   Calibration   &lt;&lt;&lt;</c>
<c>* =======================</c>
<c>* </c>
<c>* -----------------------------</c>
<c>* ===   Two caltab images   ===</c>
<c>* -----------------------------</c>
<c>* Assume that the two cameras are already calibrated (internal camera parameters)</c>
<l>CamParam1 := [0.01619,-734.789,7.402e-006,7.4e-006,324.911,256.894,640,480]</l>
<l>CamParam2 := [0.0162584,-763.35,7.39842e-006,7.4e-006,324.176,245.371,640,480]</l>
<c>* </c>
<l>read_image (Image1, ImgPath + 'camera1_ref')</l>
<l>read_image (Image2, ImgPath + 'camera2_ref')</l>
<l>dev_set_window (WindowHandle1)</l>
<l>dev_display (Image1)</l>
<l>dev_set_window (WindowHandle2)</l>
<l>dev_display (Image2)</l>
<c>* </c>
<l>CaltabName := 'caltab_30mm.descr'</l>
<l>create_calib_data ('calibration_object', 2, 1, CalibDataID)</l>
<l>set_calib_data_calib_object (CalibDataID, 0, CaltabName)</l>
<c>* </c>
<l>dev_set_window (WindowHandle1)</l>
<l>set_calib_data_cam_param (CalibDataID, 0, 'area_scan_division', CamParam1)</l>
<l>find_calib_object (Image1, CalibDataID, 0, 0, 0, [], [])</l>
<l>get_calib_data_observ_points (CalibDataID, 0, 0, 0, RCoord1, CCoord1, Index1, Pose1)</l>
<l>get_calib_data_observ_contours (Caltab, CalibDataID, 'caltab', 0, 0, 0)</l>
<l>dev_set_color ('green')</l>
<l>dev_display (Caltab)</l>
<c>* </c>
<l>dev_set_window (WindowHandle2)</l>
<l>set_calib_data_cam_param (CalibDataID, 1, 'area_scan_division', CamParam2)</l>
<l>find_calib_object (Image2, CalibDataID, 1, 0, 0, [], [])</l>
<l>get_calib_data_observ_points (CalibDataID, 1, 0, 0, RCoord2, CCoord2, Index2, Pose2)</l>
<l>get_calib_data_observ_contours (Caltab, CalibDataID, 'caltab', 1, 0, 0)</l>
<l>dev_set_color ('green')</l>
<l>dev_display (Caltab)</l>
<c>* </c>
<l>disp_message (WindowHandle1, 'Calibration successful', 'window', 12, 12, 'black', 'true')</l>
<l>disp_continue_message (WindowHandle1, 'black', 'true')</l>
<l>stop ()</l>
<c>* </c>
<c>* ================================================================</c>
<c>* &gt;&gt;&gt;   Modify poses such that map_image creates images,       &lt;&lt;&lt;</c>
<c>* &gt;&gt;&gt;   which can be merged in order to form the image mosaic  &lt;&lt;&lt;</c>
<c>* ================================================================</c>
<c>* </c>
<c>* Determine the offset between the calibration plate surface and the object surface</c>
<c>* </c>
<l>DistancePlates := 0.06488</l>
<l>ThicknessCaliper := 2.9 / 1000.0</l>
<l>ThicknessPlate := 5.65 / 1000.0</l>
<l>DiffHeight := ThicknessPlate - ThicknessCaliper</l>
<c>* </c>
<c>* Define the pixel size of the mosaic image</c>
<c>* </c>
<l>PixelSize := 0.0001</l>
<c>* </c>
<c>* Define the upper left corner of the mosaic image and the size of the two mapped images</c>
<c>* </c>
<l>BorderInPercent := 7</l>
<l>OverlapInPercent := 20</l>
<l>get_image_size (Image1, WidthImage1, HeightImage1)</l>
<l>ULRow := HeightImage1 * BorderInPercent / 100.0</l>
<l>ULCol := WidthImage1 * BorderInPercent / 100.0</l>
<l>image_points_to_world_plane (CamParam1, Pose1, ULRow, ULCol, 'm', ULX, ULY)</l>
<l>LowerRow := HeightImage1 * (100 - BorderInPercent) / 100.0</l>
<l>RightCol := WidthImage1 * (100 - OverlapInPercent / 2.0) / 100.0</l>
<l>image_points_to_world_plane (CamParam1, Pose1, LowerRow, ULCol, 'm', X1, LowerY)</l>
<l>image_points_to_world_plane (CamParam1, Pose1, ULRow, RightCol, 'm', RightX, Y1)</l>
<l>HeightRect := int((LowerY - ULY) / PixelSize)</l>
<l>WidthRect := int((RightX - ULX) / PixelSize)</l>
<c>* --------------------------------------------------------------------</c>
<c>* ===   Translate pose of left camera into plane of measuerement   ===</c>
<c>* --------------------------------------------------------------------</c>
<l>set_origin_pose (Pose1, ULX, ULY, DiffHeight, PoseNewOrigin1)</l>
<l>gen_image_to_world_plane_map (MapSingle1, CamParam1, PoseNewOrigin1, Width, Height, WidthRect, HeightRect, PixelSize, 'bilinear')</l>
<c>* </c>
<c>* ----------------------------------------------------------------</c>
<c>* ===   Merge right image in coordinate system of left image   ===</c>
<c>* ----------------------------------------------------------------</c>
<l>hom_mat3d_identity (HomMat3DIdentity)</l>
<l>hom_mat3d_translate_local (HomMat3DIdentity, ULX + PixelSize * WidthRect, ULY, DiffHeight, cp1Hur1)</l>
<l>hom_mat3d_translate_local (HomMat3DIdentity, DistancePlates, 0, 0, cp1Hcp2)</l>
<l>hom_mat3d_invert (cp1Hcp2, cp2Hcp1)</l>
<l>hom_mat3d_compose (cp2Hcp1, cp1Hur1, cp2Hul2)</l>
<l>pose_to_hom_mat3d (Pose2, cam2Hcp2)</l>
<l>hom_mat3d_compose (cam2Hcp2, cp2Hul2, cam2Hul2)</l>
<l>hom_mat3d_to_pose (cam2Hul2, PoseNewOrigin2)</l>
<l>gen_image_to_world_plane_map (MapSingle2, CamParam2, PoseNewOrigin2, Width, Height, WidthRect, HeightRect, PixelSize, 'bilinear')</l>
<c>* </c>
<c>* </c>
<c>* </c>
<c>* ==================================</c>
<c>* &gt;&gt;&gt;   Window of merged image   &lt;&lt;&lt;</c>
<c>* ==================================</c>
<l>dev_open_window (Height * WindowScale, 0, Width * 2 * WindowScale, Height * WindowScale, 'black', WindowHandleCombined)</l>
<l>set_display_font (WindowHandleCombined, 16, 'mono', 'true', 'false')</l>
<l>dev_set_color ('green')</l>
<l>dev_set_draw ('margin')</l>
<l>ScalePlot := 200</l>
<l>RowPlot := 400</l>
<l>Coord := [0:2000]</l>
<c>* </c>
<c>* </c>
<c>* </c>
<c>* =============================</c>
<c>* &gt;&gt;&gt;   Start Measurement   &lt;&lt;&lt;</c>
<c>* =============================</c>
<c>* </c>
<l>for I := 1 to 3 by 1</l>
<l>    dev_set_window (WindowHandle1)</l>
<l>    read_image (Image1, ImgPath + 'camera1_' + I$'02d')</l>
<l>    get_image_size (Image1, WidthImage1, HeightImage1)</l>
<l>    dev_set_part (0, 0, HeightImage1 - 1, WidthImage1 - 1)</l>
<l>    dev_display (Image1)</l>
<l>    dev_set_window (WindowHandle2)</l>
<l>    read_image (Image2, ImgPath + 'camera2_' + I$'02d')</l>
<l>    get_image_size (Image2, WidthImage2, HeightImage2)</l>
<l>    dev_set_part (0, 0, HeightImage2 - 1, WidthImage2 - 1)</l>
<l>    dev_display (Image2)</l>
<c>    * ===  Time measurement   ===</c>
<l>    count_seconds (TimeStart1)</l>
<l>    map_image (Image1, MapSingle1, RectifiedImage1)</l>
<l>    map_image (Image2, MapSingle2, RectifiedImage2)</l>
<l>    concat_obj (RectifiedImage1, RectifiedImage2, Concat)</l>
<l>    count_seconds (TimeEnd1)</l>
<l>    Time1 := TimeEnd1 - TimeStart1</l>
<c>    * </c>
<l>    dev_set_window (WindowHandleCombined)</l>
<c>    * ===  Time measurement   ===</c>
<l>    count_seconds (TimeStart2)</l>
<l>    tile_images (Concat, Combined, 2, 'vertical')</l>
<l>    count_seconds (TimeEnd2)</l>
<l>    Time2 := TimeEnd2 - TimeStart2</l>
<l>    get_image_size (Combined, WidthComb, HeightComb)</l>
<l>    dev_set_part (0, 0, HeightComb - 1, WidthComb - 1)</l>
<l>    dev_display (Combined)</l>
<l>    disp_message (WindowHandle1, 'Merge cameras: ' + (1000 * (Time1 + Time2))$'.3' + ' ms', 'window', 12, 12, 'black', 'true')</l>
<c>    * </c>
<l>    plot_mosaicking_accuracy (Combined, WidthRect, HeightRect, WindowHandleCombined, Coord, ScalePlot, RowPlot)</l>
<l>    if (I &lt; 3)</l>
<l>        disp_continue_message (WindowHandleCombined, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
<procedure name="plot_mosaicking_accuracy">
<interface>
<io>
<par name="Combined" base_type="iconic" dimension="0"/>
</io>
<ic>
<par name="MapWidth" base_type="ctrl" dimension="0"/>
<par name="MapHeight" base_type="ctrl" dimension="0"/>
<par name="WindowHandleCombined" base_type="ctrl" dimension="0"/>
<par name="Coord" base_type="ctrl" dimension="0"/>
<par name="ScalePlot" base_type="ctrl" dimension="0"/>
<par name="RowPlot" base_type="ctrl" dimension="0"/>
</ic>
</interface>
<body>
<c>* Segment graduation lines in combined image</c>
<l>full_domain (Combined, Combined)</l>
<l>mean_image (Combined, ImageMean, 9, 9)</l>
<l>dyn_threshold (Combined, ImageMean, RegionDynThresh, 6, 'dark')</l>
<l>connection (RegionDynThresh, ConnectedRegions)</l>
<l>select_shape (ConnectedRegions, SelectedRegions, ['anisometry','anisometry'], 'or', [10,0], [70,0])</l>
<l>select_shape (SelectedRegions, SelectedRegions3, 'area', 'and', 10, 300)</l>
<l>count_obj (SelectedRegions3, Number)</l>
<l>if (Number &gt; 10)</l>
<l>    elliptic_axis (SelectedRegions3, Ra, Rb, Phi)</l>
<l>    Angle := sort(Phi)[|Phi| / 2]</l>
<l>    select_shape (SelectedRegions3, SelectedRegions1, ['phi','phi'], 'or', [Angle - rad(5),Angle - rad(185)], [Angle + rad(5),Angle - rad(175)])</l>
<c>    * Generate measure rectangle and measure distances</c>
<c>    * between graduation lines.</c>
<l>    union1 (SelectedRegions1, RegionUnion)</l>
<l>    gen_rectangle2 (Morph, 20, 20, Angle + rad(90), 20, 3)</l>
<l>    closing (RegionUnion, Morph, RegionClosing)</l>
<l>    connection (RegionClosing, ConnectedRegions1)</l>
<l>    select_shape_std (ConnectedRegions1, SelectedRegions2, 'max_area', 0)</l>
<l>    smallest_rectangle2 (SelectedRegions2, Row, Column, Phi1, Length1, Length2)</l>
<l>    gen_rectangle2 (Rectangle, Row, Column, Phi1, max([min([Length1 - 15,700]),5]), Length2 / 2)</l>
<l>    gen_measure_rectangle2 (Row, Column, Phi1, max([min([Length1 - 15,700]),5]), Length2 / 2, MapWidth * 2, MapHeight, 'bilinear', MeasureHandle)</l>
<l>    measure_pairs (Combined, MeasureHandle, 1, 5, 'negative', 'all', RowEdgeFirst, ColumnEdgeFirst, AmplitudeFirst, RowEdgeSecond, ColumnEdgeSecond, AmplitudeSecond, IntraDistance, InterDistance)</l>
<l>    close_measure (MeasureHandle)</l>
<c>    * Plot measurement error</c>
<l>    if (|RowEdgeFirst| &gt; 10)</l>
<l>        Dist := (IntraDistance[0:|IntraDistance| - 2] + InterDistance) / 10.0</l>
<l>        dev_display (Combined)</l>
<l>        dev_set_color ('green')</l>
<l>        dev_set_line_width (2)</l>
<l>        dev_display (Rectangle)</l>
<l>        Mean := mean(Dist)</l>
<l>        Deviation := deviation(Dist)</l>
<l>        Message := 'Mean Distance: ' + Mean$'.4' + ' mm'</l>
<l>        Message[1] := 'Deviation:     ' + (Deviation * 1000)$'.4' + ' µm'</l>
<l>        Message[2] := 'Max. Error:    ' + (max(abs(Dist - 1)) * 1000)$'.4' + ' µm'</l>
<l>        disp_message (WindowHandleCombined, Message, 'window', 12, 12, 'black', 'true')</l>
<l>        Coord2 := ColumnEdgeFirst[0:|ColumnEdgeFirst| - 2] / 10</l>
<l>        if (abs(max(Dist - 1)) &lt; 0.09)</l>
<l>            gen_contour_polygon_xld (ErrorFunction, (Dist - 1) * 10 * ScalePlot + RowPlot, Coord2 * 10)</l>
<l>            gen_contour_polygon_xld (ErrorZero, Dist * 0 + RowPlot, Coord2 * 10)</l>
<l>            gen_contour_polygon_xld (ErrorMin01mm, Dist * 0 - 0.1 * ScalePlot + RowPlot, Coord2 * 10)</l>
<l>            gen_contour_polygon_xld (ErrorPlus01mm, Dist * 0 + 0.1 * ScalePlot + RowPlot, Coord2 * 10)</l>
<l>            for I := 0 to |ColumnEdgeSecond| - 1 by 1</l>
<l>                if (ColumnEdgeSecond[I] &gt; MapWidth)</l>
<l>                    break</l>
<l>                endif</l>
<l>            endfor</l>
<l>            gen_contour_polygon_xld (UpperMarkForSeem, [-0.1 * ScalePlot + RowPlot,-0.15 * ScalePlot + RowPlot], [659.5,659.5])</l>
<l>            gen_contour_polygon_xld (LowerMarkForSeem, [0.1 * ScalePlot + RowPlot,0.15 * ScalePlot + RowPlot], [659.5,659.5])</l>
<l>            dev_set_color ('cyan')</l>
<l>            dev_display (ErrorZero)</l>
<l>            dev_display (ErrorPlus01mm)</l>
<l>            dev_display (ErrorMin01mm)</l>
<l>            dev_display (UpperMarkForSeem)</l>
<l>            dev_display (LowerMarkForSeem)</l>
<l>            if (RowEdgeFirst[0] &lt; RowEdgeFirst[|RowEdgeFirst| - 1])</l>
<l>                ColumnText := ColumnEdgeFirst[0]</l>
<l>            else</l>
<l>                ColumnText := ColumnEdgeFirst[|ColumnEdgeFirst| - 1] - 120</l>
<l>            endif</l>
<l>            disp_message (WindowHandleCombined, '+0.01mm', 'image', RowPlot - 51, ColumnText, 'cyan', 'false')</l>
<l>            disp_message (WindowHandleCombined, '-0.01mm', 'image', RowPlot + 18, ColumnText, 'cyan', 'false')</l>
<l>            dev_set_color ('yellow')</l>
<l>            dev_set_line_width (2)</l>
<l>            dev_display (ErrorFunction)</l>
<l>*             dev_set_line_width (1)</l>
<l>*             dev_set_color ('white')</l>
<l>*             gen_arrow_contour_xld (Arrow, RowEdgeSecond[I - 1], 659.5, -0.16 * ScalePlot + RowPlot, 659.5, 5, 5)</l>
<l>*             dev_display (Arrow)</l>
<l>        endif</l>
<l>    else</l>
<l>        dev_display (Combined)</l>
<l>    endif</l>
<l>else</l>
<l>    dev_display (Combined)</l>
<l>endif</l>
<l>return ()</l>
</body>
<docu id="plot_mosaicking_accuracy">
<short lang="en_US">Tests the accuracy of the mosaic image by measuring the distance between the scale lines.</short>
<parameters>
<parameter id="Combined"/>
<parameter id="Coord"/>
<parameter id="MapHeight"/>
<parameter id="MapWidth"/>
<parameter id="RowPlot"/>
<parameter id="ScalePlot"/>
<parameter id="WindowHandleCombined"/>
</parameters>
</docu>
</procedure>
</hdevelop>
