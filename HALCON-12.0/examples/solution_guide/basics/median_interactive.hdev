<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* median_interactive.hdev: react to mouse interaction</c>
<c>* </c>
<l>dev_set_preferences ('graphics_window_context_menu', 'false')</l>
<l>dev_close_window ()</l>
<l>dev_update_off ()</l>
<c>* ****</c>
<c>* step: acquire image</c>
<c>* ****</c>
<l>read_image (Image, 'mreut')</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_open_window_fit_image (Image, 0, 0, Width, Height, WindowHandle)</l>
<l>set_display_font (WindowHandle, 12, 'mono', 'true', 'false')</l>
<l>set_mshape (WindowHandle, 'arrow')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (3)</l>
<l>dev_display (Image)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* ****</c>
<c>* step: react to mouse</c>
<c>* ****</c>
<l>Button := 0</l>
<l>while (Button != 4)</l>
<l>    Row := -1</l>
<l>    Column := -1</l>
<l>    dev_set_check ('~give_error')</l>
<l>    get_mposition (WindowHandle, Row, Column, Button)</l>
<l>    dev_set_check ('give_error')</l>
<l>    set_system ('flush_graphic', 'false')</l>
<l>    dev_display (Image)</l>
<l>    set_tposition (WindowHandle, Height - 40, 4)</l>
<c>    * check if mouse cursor is over window</c>
<l>    if (Row &gt;= 0 and Column &gt;= 0)</l>
<l>        gen_circle (Circle, Row, Column, 20)</l>
<l>        boundary (Circle, RegionBorder, 'inner')</l>
<l>        dev_display (RegionBorder)</l>
<l>        set_system ('flush_graphic', 'true')</l>
<l>        write_string (WindowHandle, 'Hold down mouse button to apply filter')</l>
<l>        if (Button == 1)</l>
<c>            * apply filter to circle if left button is pressed</c>
<l>            reduce_domain (Image, Circle, ImageReduced)</l>
<l>            median_image (ImageReduced, ImageMedian, 'circle', 5, 'mirrored')</l>
<l>            overpaint_gray (Image, ImageMedian)</l>
<l>        endif</l>
<l>    else</l>
<l>        set_system ('flush_graphic', 'true')</l>
<l>        write_string (WindowHandle, 'Please move mouse cursor over window')</l>
<l>    endif</l>
<l>endwhile</l>
<l>dev_display (Image)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
