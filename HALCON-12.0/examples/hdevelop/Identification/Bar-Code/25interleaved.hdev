<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* Read bar codes of type 2/5 Interleaved</c>
<c>* </c>
<l>create_bar_code_model ([], [], BarCodeHandle)</l>
<c>* We expect to decode a single bar code per image</c>
<l>set_bar_code_param (BarCodeHandle, 'stop_after_result_num', 1)</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 120, 300, 'black', WindowHandle)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_line_width (3)</l>
<l>for I := 1 to 8 by 1</l>
<l>    read_image (Image, 'barcode/25interleaved/25interleaved' + (I$'.2'))</l>
<l>    get_image_size (Image, Width, Height)</l>
<l>    dev_set_window_extents (0, 0, Width - 1, Height - 1)</l>
<l>    dev_display (Image)</l>
<l>    dev_set_color ('green')</l>
<c>    * Read bar code, the resulting string includes the check character</c>
<l>    set_bar_code_param (BarCodeHandle, 'check_char', 'absent')</l>
<l>    find_bar_code (Image, SymbolRegions, BarCodeHandle, '2/5 Interleaved', DecodedDataStrings)</l>
<l>    disp_message (WindowHandle, DecodedDataStrings, 'window', 12, 12, 'black', 'false')</l>
<l>    LastChar := strlen(DecodedDataStrings) - 1</l>
<l>    disp_message (WindowHandle, sum(gen_tuple_const(LastChar,' ')) + DecodedDataStrings{LastChar}, 'window', 12, 12, 'forest green', 'false')</l>
<l>    stop ()</l>
<c>    * Read bar code using the check character to check the result, i.e.,</c>
<c>    * the check character does not belong to the returned string anymore.</c>
<c>    * If the check character is not correct, the bar code reading fails</c>
<l>    dev_set_color ('green')</l>
<l>    set_bar_code_param (BarCodeHandle, 'check_char', 'present')</l>
<l>    find_bar_code (Image, SymbolRegions, BarCodeHandle, '2/5 Interleaved', DecodedDataStrings)</l>
<l>    disp_message (WindowHandle, DecodedDataStrings, 'window', 36, 12, 'black', 'false')</l>
<l>    if (I &lt; 8)</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
<l>clear_bar_code_model (BarCodeHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
