<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* This example program shows how to segment an RGB image with a k-NN</c>
<c>* classifier.  The classifier is trained with four different colors.  Note that the</c>
<c>* resulting classification is intuitively correct.  However, it can also be seen</c>
<c>* that colors that lie outside of the range of the trained classes will be assigned</c>
<c>* to one of the classes with a high confidence.  In this image, the yellow paint</c>
<c>* at the ship's rail, the green color on the light of the top of the ship, and the door</c>
<c>* are assigned to one of the four classes with a high confidence.</c>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>dev_open_window (0, 0, 735, 485, 'black', WindowHandle)</l>
<l>set_display_font (WindowHandle, 14, 'mono', 'true', 'false')</l>
<l>dev_set_draw ('margin')</l>
<l>dev_set_colored (6)</l>
<l>dev_set_line_width (3)</l>
<l>read_image (Image, 'patras')</l>
<l>dev_display (Image)</l>
<l>Color := ['indian red','cornflower blue','white','black','yellow']</l>
<c>* Create regions that contain the training samples of the four classes</c>
<l>gen_rectangle1 (Sea, 10, 10, 120, 270)</l>
<l>gen_rectangle2 (Deck, [170,400], [350,375], [-0.56,-0.75], [64,104], [26,11])</l>
<l>union1 (Deck, Deck)</l>
<l>gen_rectangle1 (Walls, 355, 623, 420, 702)</l>
<l>gen_rectangle2 (Chimney, 286, 623, -0.56, 64, 33)</l>
<l>concat_obj (Sea, Deck, Classes)</l>
<l>concat_obj (Classes, Walls, Classes)</l>
<l>concat_obj (Classes, Chimney, Classes)</l>
<l>dev_set_color (Color[0])</l>
<l>dev_display (Deck)</l>
<l>dev_set_color (Color[1])</l>
<l>dev_display (Sea)</l>
<l>dev_set_color (Color[2])</l>
<l>dev_display (Walls)</l>
<l>dev_set_color (Color[3])</l>
<l>dev_display (Chimney)</l>
<l>Message := 'Training regions for the color classifier'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* Create the classifier and add the samples</c>
<l>create_class_knn (3, KNNHandle)</l>
<l>add_samples_image_class_knn (Image, Classes, KNNHandle)</l>
<l>dev_display (Image)</l>
<l>Message := 'Training ...'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<c>* Train the classifier</c>
<l>train_class_knn (KNNHandle, [], [])</l>
<l>Message := Message + ' ready.'</l>
<l>Message[1] := 'Segment image using the classifier ...'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<c>* Segment (classify) the image</c>
<l>classify_image_class_knn (Image, ClassRegions, DistanceImage, KNNHandle, 255.0 * 255.0)</l>
<l>region_to_mean (ClassRegions, Image, ImageClass)</l>
<l>dev_display (ImageClass)</l>
<l>Message[1] := Message[1] + ' ready.'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>clear_class_knn (KNNHandle)</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* Since the yellow paint at the ship's rail, the green color on the top light,</c>
<c>* and the door are unintentionally assigned to one of the four trained classes, we</c>
<c>* will construct an explicit rejection class that contains samples from each unwanted</c>
<c>* color and will re-train the classifier with the rejection class.</c>
<l>gen_rectangle2 (Rejection, [193,66,261], [235,332,328], [-0.32,-1.45,-1.51], [33,34,60], [4,3,3])</l>
<l>union1 (Rejection, Rejection)</l>
<l>concat_obj (Classes, Rejection, Classes)</l>
<l>dev_display (Image)</l>
<l>dev_set_color (Color[0])</l>
<l>dev_display (Deck)</l>
<l>dev_set_color (Color[1])</l>
<l>dev_display (Sea)</l>
<l>dev_set_color (Color[2])</l>
<l>dev_display (Walls)</l>
<l>dev_set_color (Color[3])</l>
<l>dev_display (Chimney)</l>
<l>dev_set_color (Color[4])</l>
<l>dev_display (Rejection)</l>
<l>disp_message (WindowHandle, 'Add a rejection class to improve the robustness of the classifier', 'window', 12, 12, 'black', 'true')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<l>dev_display (Image)</l>
<c>* Create the classifier and add the samples</c>
<l>create_class_knn (3, KNNHandle)</l>
<l>add_samples_image_class_knn (Image, Classes, KNNHandle)</l>
<c>* Train the classifier</c>
<l>Message := 'Training the classifier with rejection class...'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>train_class_knn (KNNHandle, [], [])</l>
<l>Message := Message + ' ready.'</l>
<l>Message[1] := 'Segment image using the classifier ...'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<c>* Segment (classify) the image</c>
<l>classify_image_class_knn (Image, ClassRegionsNotRejected, DistanceImage1, KNNHandle, 255.0 * 255.0)</l>
<c>* Select every class except the rejection class</c>
<l>copy_obj (ClassRegionsNotRejected, ClassRegionsNotRejected, 1, 4)</l>
<c>* Note that the black areas in the mean image correspond to the rejected pixels</c>
<l>region_to_mean (ClassRegionsNotRejected, Image, ImageClassNotRejected)</l>
<l>dev_display (ImageClassNotRejected)</l>
<l>Message[1] := Message[1] + ' ready.'</l>
<l>disp_message (WindowHandle, Message, 'window', 12, 12, 'black', 'true')</l>
<l>clear_class_knn (KNNHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
