<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* This example shows how to use OCR in combination with regular expressions</c>
<c>* to correct potential misclassifications between similar characters</c>
<c>* like 'D' and '0', '0' (zero) and 'O' (capital 'o').</c>
<c>* This is very useful in applications where one has to read fixed types of expressions</c>
<c>* like expiration dates, or lot numbers.</c>
<c>* </c>
<l>dev_close_window ()</l>
<l>dev_update_pc ('off')</l>
<l>dev_update_window ('off')</l>
<l>read_image (Image, 'ocr/lot_number_01')</l>
<l>get_image_size (Image, Width, Height)</l>
<l>dev_open_window (0, 0, Width, Height, 'black', WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_set_draw ('margin')</l>
<l>create_text_model_reader ('manual', [], TextModel)</l>
<c>* </c>
<l>set_text_model_param (TextModel, 'char_width', 45)</l>
<l>set_text_model_param (TextModel, 'char_height', 80)</l>
<l>set_text_model_param (TextModel, 'is_dotprint', 'true')</l>
<l>set_text_model_param (TextModel, 'stroke_width', 7)</l>
<l>set_text_model_param (TextModel, 'uppercase_only', 'true')</l>
<l>set_text_model_param (TextModel, 'max_line_num', 1)</l>
<c>* </c>
<l>read_ocr_class_mlp ('DotPrint', OCRHandle)</l>
<l>for Index := 1 to 12 by 1</l>
<l>    read_image (Image, 'ocr/lot_number_' + Index$'.2')</l>
<l>    dev_display (Image)</l>
<c>    * </c>
<c>    * Segmentation of characters</c>
<l>    find_text (Image, TextModel, TextResultID)</l>
<l>    get_text_object (Characters, TextResultID, 'all_lines')</l>
<l>    clear_text_result (TextResultID)</l>
<c>    * </c>
<l>    do_ocr_word_mlp (Characters, Image, OCRHandle, '\\d{5}[A-Z]{1}', 3, 2, Class, Confidence, Word, Score)</l>
<c>    * </c>
<l>    dev_set_line_width (2)</l>
<l>    dev_display (Image)</l>
<l>    dev_set_colored (12)</l>
<l>    dev_display (Characters)</l>
<l>    dev_set_draw ('margin')</l>
<l>    disp_message (WindowHandle, 'Lot number: ' + Word, 'window', 12, 12, 'black', 'true')</l>
<l>    if (Index &lt; 12)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>    endif</l>
<l>    stop ()</l>
<l>endfor</l>
<l>clear_ocr_class_mlp (OCRHandle)</l>
<l>clear_text_model (TextModel)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
