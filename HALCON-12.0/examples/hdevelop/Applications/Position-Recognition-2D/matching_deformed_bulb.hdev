<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* This example program shows the use of the operator find_shape_model</c>
<c>* with the increased tolerance mode that allows to find matches even in</c>
<c>* defocused images or images that show deformed objects.</c>
<c>* </c>
<c>* In this example, the leads of halogen bulbs must be detected.</c>
<c>* Unfortunately, the geometry of the leads varies slightly and the</c>
<c>* reflections from the glas body distort the image edges even more.</c>
<c>* If find_shape_model is used in standard mode, it will not find</c>
<c>* each instance of the object, because the differences between the</c>
<c>* model and the image edges are too big.</c>
<c>* If, instead, the last pyramid level is specified negatively,</c>
<c>* find_shape_model will return the match on the lowest pyramid level</c>
<c>* on which a match was found. In this mode, larger deformations</c>
<c>* are tolerated as if the match must be found on the lowest</c>
<c>* level of the image pyramid.</c>
<c>* </c>
<l>list_image_files ('halogen_bulb', [], 'recursive', ImageFiles)</l>
<l>tuple_regexp_select (ImageFiles, 'good', ImageFilesGood)</l>
<l>read_image (Image, ImageFilesGood[0])</l>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>dev_open_window_fit_image (Image, 0, 0, 640, 640, WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_set_color ('lime green')</l>
<l>dev_set_line_width (3)</l>
<c>* </c>
<c>* Create the shape model</c>
<l>gen_rectangle1 (Rectangle, 400, 300, 900, 1000)</l>
<l>reduce_domain (Image, Rectangle, ImageReduced)</l>
<l>create_shape_model (ImageReduced, 'auto', -0.39, 0.78, 'auto', 'auto', 'use_polarity', 'auto', 'auto', ModelID)</l>
<l>area_center (Rectangle, Area, Row, Column)</l>
<l>dev_clear_window ()</l>
<l>dev_display (Image)</l>
<l>dev_display_shape_matching_results (ModelID, 'lime green', Row, Column, 0.0, 1.0, 1.0, 0)</l>
<l>disp_message (WindowHandle, 'The model edges used for the matching', 'window', -1, -1, 'lime green', 'false')</l>
<l>disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>stop ()</l>
<c>* </c>
<l>for Index := 0 to |ImageFiles| - 1 by 1</l>
<l>    if (regexp_match(ImageFiles[Index],'none') == 'none')</l>
<l>        continue</l>
<l>    endif</l>
<l>    read_image (Image, ImageFiles[Index])</l>
<l>    count_seconds (Seconds1)</l>
<l>    find_shape_model (Image, ModelID, -0.39, 0.78, 0.7, 1, 0.5, 'least_squares', [0,-1], 0.9, Row, Column, Angle, Score)</l>
<l>    count_seconds (Seconds2)</l>
<l>    dev_display (Image)</l>
<l>    dev_display_shape_matching_results (ModelID, 'lime green', Row, Column, Angle, 1.0, 1.0, 0)</l>
<l>    disp_message (WindowHandle, |Row| + ' Match found in ' + ((Seconds2 - Seconds1) * 1000.0)$'.1f' + ' ms', 'window', -1, -1, 'lime green', 'false')</l>
<l>    if (Index &lt; |ImageFiles| - 1)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
<l>clear_shape_model (ModelID)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
