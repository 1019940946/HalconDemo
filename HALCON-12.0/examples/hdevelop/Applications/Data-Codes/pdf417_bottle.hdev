<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* This programs shows how to read 2d data codes of type PDF 417.</c>
<c>* </c>
<l>dev_update_off ()</l>
<l>dev_close_window ()</l>
<l>read_image (Image, 'datacode/pdf417/pdf417_bottle_01')</l>
<l>dev_open_window_fit_image (Image, 0, 0, -1, -1, WindowHandle)</l>
<l>set_display_font (WindowHandle, 16, 'mono', 'true', 'false')</l>
<l>dev_set_line_width (3)</l>
<l>gen_rectangle1 (Rectangle, 250, 0, 600, 610)</l>
<l>dev_set_color ('green')</l>
<c>* </c>
<c>* Create a 2d data code model</c>
<l>create_data_code_2d_model ('PDF417', [], [], DataCodeHandle)</l>
<l>set_data_code_2d_param (DataCodeHandle, 'default_parameters', 'maximum_recognition')</l>
<c>* </c>
<l>NumImages := 6</l>
<l>for Index := 1 to NumImages by 1</l>
<c>    * Read the image and decode the 2d data code</c>
<l>    read_image (Image, 'datacode/pdf417/pdf417_bottle_' + Index$'02')</l>
<l>    reduce_domain (Image, Rectangle, ImageReduced)</l>
<l>    count_seconds (T1)</l>
<l>    find_data_code_2d (ImageReduced, SymbolXLDs, DataCodeHandle, [], [], ResultHandles, DecodedDataStrings)</l>
<l>    count_seconds (T2)</l>
<l>    Time := 1000 * (T2 - T1)</l>
<c>    * </c>
<c>    * Display the runtime and the found symbols</c>
<l>    dev_set_window (WindowHandle)</l>
<l>    dev_display (Image)</l>
<l>    dev_display (SymbolXLDs)</l>
<l>    disp_message (WindowHandle, 'Data code found in ' + Time$'.1f' + ' ms', 'window', 12, 12, 'black', 'true')</l>
<l>    for I := 1 to |ResultHandles| by 1</l>
<l>        Length := strlen(DecodedDataStrings[I - 1])</l>
<l>        Message := ['Value:',DecodedDataStrings[I - 1]{0:(Length / 2) - 1},DecodedDataStrings[I - 1]{Length / 2:Length - 1}]</l>
<l>        disp_message (WindowHandle, Message, 'image', (I + 1) * 24, 12, 'black', 'true')</l>
<l>    endfor</l>
<l>    if (Index != NumImages)</l>
<l>        disp_continue_message (WindowHandle, 'black', 'true')</l>
<l>        stop ()</l>
<l>    endif</l>
<l>endfor</l>
<c>* </c>
<c>* Clear the 2d data code model</c>
<l>clear_data_code_2d_model (DataCodeHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
