<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.1" halcon_version="12.0">
<procedure name="main">
<interface/>
<body>
<c>* </c>
<c>* Example for grabbing images from a IDS uEye camera</c>
<c>* (hardware/software triggering)</c>
<c>* </c>
<c>* </c>
<c>* Open first uEye camera</c>
<l>open_framegrabber ('uEye', 1, 1, 0, 0, 0, 0, 'default', -1, 'default', -1, 'false', 'default', 'default', -1, -1, AcqHandle)</l>
<c>* </c>
<l>get_framegrabber_param (AcqHandle, 'revision', Revision)</l>
<l>get_framegrabber_param (AcqHandle, 'external_trigger', ExternalTrigger)</l>
<l>get_framegrabber_param (AcqHandle, 'trigger_signal', TriggerSignal)</l>
<l>get_framegrabber_param (AcqHandle, 'grab_timeout', GrabTimeout)</l>
<l>get_framegrabber_param (AcqHandle, 'pixel_clock', PixelClock)</l>
<l>get_framegrabber_param (AcqHandle, 'exposure', Exposure)</l>
<c>* </c>
<c>* </c>
<l>dev_close_window ()</l>
<l>get_framegrabber_param (AcqHandle, ['image_width','image_height'], ImageSize)</l>
<l>dev_open_window (0, 0, ImageSize[0] / 2, ImageSize[1] / 2, 'black', WindowID)</l>
<c>* </c>
<c>* </c>
<c>* (A) Hardware trigger falling edge</c>
<l>set_framegrabber_param (AcqHandle, 'external_trigger', 'true')</l>
<l>set_framegrabber_param (AcqHandle, 'trigger_signal', 'falling')</l>
<l>grab_image_start (AcqHandle, -1)</l>
<l>count_seconds (SecondsOld)</l>
<l>for n := 1 to 50 by 1</l>
<l>    grab_image_async (Image, AcqHandle, -1)</l>
<l>    dev_display (Image)</l>
<l>    count_seconds (SecondsNew)</l>
<l>    FrameRate := 1 / (SecondsNew - SecondsOld)</l>
<l>    SecondsOld := SecondsNew</l>
<l>endfor</l>
<c>* </c>
<c>* </c>
<c>* (B) Software trigger</c>
<c>* Method 1: using 'do_force_trigger'</c>
<l>set_framegrabber_param (AcqHandle, 'external_trigger', 'true')</l>
<l>grab_image_start (AcqHandle, -1)</l>
<l>count_seconds (SecondsOld)</l>
<l>for n := 1 to 50 by 1</l>
<l>    set_framegrabber_param (AcqHandle, 'do_force_trigger', 1)</l>
<l>    grab_image_async (Image, AcqHandle, -1)</l>
<l>    dev_display (Image)</l>
<l>    count_seconds (SecondsNew)</l>
<l>    FrameRate := 1 / (SecondsNew - SecondsOld)</l>
<l>    SecondsOld := SecondsNew</l>
<l>endfor</l>
<c>* </c>
<c>* </c>
<c>* (B) Software trigger</c>
<c>* Method 2: using 'trigger_signal' = 'software'</c>
<l>set_framegrabber_param (AcqHandle, 'external_trigger', 'true')</l>
<l>set_framegrabber_param (AcqHandle, 'trigger_signal', 'software')</l>
<l>count_seconds (SecondsOld)</l>
<l>for n := 1 to 50 by 1</l>
<l>    grab_image (Image, AcqHandle)</l>
<l>    dev_display (Image)</l>
<l>    count_seconds (SecondsNew)</l>
<l>    FrameRate := 1 / (SecondsNew - SecondsOld)</l>
<l>    SecondsOld := SecondsNew</l>
<l>endfor</l>
<c>* </c>
<c>* </c>
<c>* Close uEye camera</c>
<l>close_framegrabber (AcqHandle)</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
